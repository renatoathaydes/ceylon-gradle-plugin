plugins {
    id "com.jfrog.bintray" version "1.4"
    id 'maven-publish'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.9.3'
}

group 'com.athaydes.gradle.ceylon'
version '1.1.2'
sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

/* Publishing config */

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
        }
    }
}

// Gradle Plugin Repository

pluginBundle {
    website = 'https://github.com/renatoathaydes/ceylon-gradle-plugin'
    vcsUrl = 'https://github.com/renatoathaydes/ceylon-gradle-plugin'
    description = 'A simple Gradle plugin to manage Ceylon projects'
    tags = ['ceylon', 'language']

    plugins {
        ceylonPlugin {
            id = 'com.athaydes.ceylon'
            displayName = 'Ceylon Gradle Plugin'
        }
    }
}

bintray {
    user = project.hasProperty( "bintrayUserName" ) ? bintrayUserName : null
    key = project.hasProperty( "bintrayApiKey" ) ? bintrayApiKey : null
    publications = [ "mavenJava" ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'com.athaydes.ceylon'
        licenses = [ 'Apache-2.0' ]
        desc = 'A Gradle plugin to make it easy to handle Ceylon projects with Gradle'
        vcsUrl = 'https://github.com/renatoathaydes/ceylon-gradle-plugin.git'
        websiteUrl = 'https://github.com/renatoathaydes/ceylon-gradle-plugin'
        issueTrackerUrl = 'https://github.com/renatoathaydes/ceylon-gradle-plugin/issues'
        labels = [ 'ceylon' ]
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}
